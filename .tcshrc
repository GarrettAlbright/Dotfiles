# Garrett's .tcshrc. Yes, I use TCSH instead of whatever you use. Sorry.

setenv CLICOLOR YES
set history = 5000
set correct = cmd 
set savehist = (5000 merge)
set autolist
set complete = 'igncase'
set promptchars = '>%'
set listjobs
set backslash_quote

bindkey -k up history-search-backward
bindkey -k down history-search-forward

# Show current directory in window title
# TODO: Percent escaping other chars necessary?
alias cwdcmd 'echo -n "]7;file:`pwd|sed s/\ /\%20/g`"'

# Really seriously save the history after every command
alias postcmd 'history -S'

# Create an alias that echoes a bell for use in scripts and such
alias bell echo ""

# Make ls -l show human sizes without having to use -h
alias ls ls -h

# http://stackoverflow.com/questions/14966102/minimal-typing-command-line-calculator-tcsh-vs-bash?rq=1
alias C 'echo '\''\!*'\'' |tr -d '\'',\042-\047'\'' |bc -l'

# Alias for the CLI PHP binary inside of the MAMP collection
alias mampphp /Applications/MAMP/bin/php/php7.1.8/bin/php 

# Alias for ag-ing only inside PHP files
alias agp ag -G '\.php$'

# Make XZ use all cores and compress its best
setenv XZ_OPT '--best -T `sysctl -n hw.ncpu`'

setenv NETHACKOPTIONS "NAME=Pointage,NOAUTOPICKUP,COLOR"

if ( `uname` == "Darwin" ) then
  # Mac-exclusive stuff

  # Looks like "MachineName /foo/bar> "
  set prompt = '%{\033[33m%}%m %{\033[36m%}%~%{\033[0m%}%# '
  alias plaincopy 'pbpaste -Prefer txt | tee pbcopy; echo'

  # Path for MacPorts-installed stuff
  setenv PATH "/opt/local/bin:/opt/local/sbin:$PATH"

  # Fix forward-delete
  # See comments at http://hints.macworld.com/article.php?story=20050525040921189
  bindkey "\e[3~" delete-char

  # Quickly bring dev stuff up or down
  alias webup "sudo port load nginx; sudo port load mariadb-10.1-server; sudo port load php72-fpm"
  alias webdn "sudo port unload nginx; sudo port unload mariadb-10.1-server; sudo port unload php72-fpm"

  # swiftenv stuff
  setenv SWIFTENV_ROOT "$HOME/.swiftenv"
  setenv PATH "$SWIFTENV_ROOT/bin:$PATH"
  eval "`swiftenv init - | sed -e 's/export PATH=/setenv PATH /'`"

  # Alias to quickly regenerate Xcode project files
  alias spg 'swift package generate-xcodeproj --output=`pwd | rev | cut -d "/" -f 1 | rev | tr -d " "`.xcodeproj'
else
  # BSD-exclusive stuff

  # Prompt is same as on Mac, but machine name is reverse video
  set prompt = '%S%{\033[33m%}%m%s %{\033[36m%}%~%{\033[0m%}%# '

  # Start tsch automatically
  # https://stackoverflow.com/questions/27613209/how-to-automatically-start-tmux-on-ssh-session
  if ( "$prompt" != "" && ! $?TMUX && "$SSH_CONNECTION" != "" ) then
    tmux attach-session -t $USER || tmux new-session -s $USER
  endif
endif 

