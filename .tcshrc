# Garrett's .tcshrc. Yes, I use TCSH instead of whatever you use. Sorry.

setenv CLICOLOR YES
set history = 5000
set correct = cmd 
set savehist = 5000 merge lock
set autolist
set complete = 'igncase'
set promptchars = '>%'
set listjobs

bindkey -k up history-search-backward
bindkey -k down history-search-forward

#alias precmd 'printf "\033]0;$cwd\007"'

# Create an alias that echoes a bell for use in scripts and such
alias bell echo ""

# Make ls -l show human sizes without having to use -h
alias ls ls -h

# http://stackoverflow.com/questions/14966102/minimal-typing-command-line-calculator-tcsh-vs-bash?rq=1
alias C 'echo '\''\!*'\'' |tr -d '\'',\042-\047'\'' |bc -l'

# Make XZ use all cores and compress its best
setenv XZ_OPT "--best -T `sysctl -n hw.ncpu`"

setenv NETHACKOPTIONS "NAME=Pointage,NOAUTOPICKUP,COLOR"

if ( `uname` == "Darwin" ) then
  # Mac-exclusive stuff
  # Looks like "MachineName /foo/bar> "
  set prompt = '%{\033[33m%}%m %{\033[36m%}%~%{\033[0m%}%# '
  alias plaincopy 'pbpaste -Prefer txt | tee pbcopy; echo'
  # Path for MacPorts-installed stuff
  setenv PATH /opt/local/bin:/opt/local/sbin:$PATH
  alias mmount 'sudo mount -o resvport 192.168.50.7:/assets /assets'
  # Fix forward-delete
  # See comments at http://hints.macworld.com/article.php?story=20050525040921189
  bindkey "\e[3~" delete-char
else
  # BSD-exclusive stuff
  # Prompt is same as on Mac, but machine name is reverse video
  set prompt = '%S%{\033[33m%}%m%s %{\033[36m%}%~%{\033[0m%}%# '
endif 

